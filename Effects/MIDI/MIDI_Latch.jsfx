/*******************************************************************************
version: 1.0
desc: MIDI_Latch (note latching tool)
author: IX
provides: . > ../../MIDI_Latch
about:
  # MIDI Latch
  Latched keys hold until next keypress.
********************************************************************************
* Copyright 2008 Philip S. Considine
* License: GPL - http://www.gnu.org/licenses/gpl.html
*******************************************************************************/

desc: Latched keys hold until next keypress.

slider1:0<0,15,1{1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16}>Channel

//Note Sliders
slider2:0<0,127,1>Key

//Note drop-downs (disabled)
//slider2:0<0,127,1{C0,C#0,D0,D#0,E0,F0,F#0,G0,G#0,A0,A#0,B0,C1,C#1,D1,D#1,E1,F1,F#1,G1,G#1,A1,A#1,B1,C2,C#2,D2,D#2,E2,F2,F#2,G2,G#2,A2,A#2,B2,C3,C#3,D3,D#3,E3,F3,F#3,G3,G#3,A3,A#3,B3,C4,C#4,D4,D#4,E4,F4,F#4,G4,G#4,A4,A#4,B4,C5,C#5,D5,D#5,E5,F5,F#5,G5,G#5,A5,A#5,B5,C6,C#6,D6,D#6,E6,F6,F#6,G6,G#6,A6,A#6,B6,C7,C#7,D7,D#7,E7,F7,F#7,G7,G#7,A7,A#7,B7,C8,C#8,D8,D#8,E8,F8,F#8,G8,G#8,A8,A#8,B8,C9,C#9,D9,D#9,E9,F9,F#9,G9,G#9,A9,A#9,B9,C10,C#10,D10,D#10,E10,F10,F#10,G10}>Key

slider3:0<0,1,1{Off,On}>Latch

/////////////////////////////////////////////////////////////////////////////////////////////////
@init
ext_noinit = 1;
map = 0;
tracker = 128;

statNoteOff = $x80;
statNoteOn = $x90;

/////////////////////////////////////////////////////////////////////////////////////////////////
@serialize
file_mem(0,map,128);

/////////////////////////////////////////////////////////////////////////////////////////////////
@slider
inChannel = slider1;

slider2 != key ?
(
	key = slider2 = floor(slider2);
	slider3 = map[key];
);

slider3 != map[key] ?
	map[key] = slider3;

/////////////////////////////////////////////////////////////////////////////////////////////////
@block
trigger & (2^0) ?
(
	reset = 1;
	value = 0;
);

trigger & (2^1) ?
(
	reset = 1;
	value = 1;
);

reset ?
(
	i = 0;
	while
	(
		map[i] = value;
		tracker[i] = 0;
		i += 1;
		i < 128;
	);
	reset = 0;
);

while
(
	midirecv(offset,msg1,msg23) ?
	(
		status = msg1 & 240;
		channel = msg1 & 15;

		channel == inChannel ? 
		(
			slider2 = note; sliderchange(slider2);
			slider3 = map[note]; sliderchange(slider3);

			status == statNoteOn || status == statNoteOff ?
			(
				note = msg23 & 127;
				velocity = ((msg23/256) & 127);

				map[note] == 1 ?
				(
					status == statNoteOn && velocity > 0 ?
					(
						tracker[note] == 0 ?
						(
							midisend(offset,msg1,msg23);
							tracker[note] = 1;
						)
						:
						(
							midisend(offset,statNoteOff+channel,msg23);
							tracker[note] = 0;
						);
					);
				)
				:
				midisend(offset,msg1,msg23); //Not flagged for latch, pass thru
			)
			:
			midisend(offset,msg1,msg23); //Not a note, pass thru	
		)
		:
		midisend(offset,msg1,msg23); //Not on our channel, pass thru
	);
);


